plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

//mainClassName = "com.r3.conclave.sample.auction.host.AuctionHost"

application {
    // conclave-web-host provides us with the main method to use.
    mainClassName = "com.r3.conclave.host.web.EnclaveWebHost"
}


// Override the default (simulation) with -PenclaveMode=
def mode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "mock"

// Create a task that can be used for generating signing materials
tasks.register("prepareForSigning") {
    it.dependsOn(":enclave:generateEnclaveSigningMaterial" + mode.capitalize())
}

dependencies {
    // Use the host web server for receiving and sending mail to the clients. This means the client needs to use the
    // conclave-web-client library.
    runtimeOnly "com.r3.conclave:conclave-web-host:$conclaveVersion"

    runtimeOnly project(path: ":enclave", configuration: mode)

    // Enable unit tests
    testImplementation "com.r3.conclave:conclave-host:$conclaveVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
}

shadowJar {
    archiveAppendix.set(mode)
    archiveClassifier.set("")
}