plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

application {
    // conclave-web-host provides us with the main method to use.
    mainClassName = "com.r3.conclave.host.web.EnclaveWebHost"
}

// Override the default (mock) with -PenclaveMode=
def mode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "mock"


dependencies {
    // *your* enclave
    runtimeOnly project(path: ":enclave", configuration: mode)
    // Use the host web server for receiving and sending mail to the clients. This means the client needs to use the
    // conclave-web-client library.
    runtimeOnly "com.r3.conclave:conclave-web-host:$conclaveVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:5.6.0"
}

shadowJar {
    archiveAppendix.set(mode)
    archiveClassifier.set("")
}
