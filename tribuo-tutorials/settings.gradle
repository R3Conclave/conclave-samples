pluginManagement {
    apply from: '../versions.gradle'
    repositories {
        maven {
            def repoPath = file(rootDir.relativePath(file(conclaveRepo)))
            if (repoPath == null)
                throw new Exception("Make sure the 'conclaveRepo' setting exists in gradle.properties, or your \$HOME/gradle.properties file. See the Conclave tutorial on https://docs.conclave.net")
            else if (!new File(repoPath, "com").isDirectory())
                throw new Exception("The $repoPath directory doesn't seem to exist or isn't a Maven repository; it should be the SDK 'repo' subdirectory. See the Conclave tutorial on https://docs.conclave.net")
            url = repoPath
        }
        // Add standard repositories back.
        gradlePluginPortal()
        jcenter()
        mavenCentral()
    }

    plugins {
        id 'org.jetbrains.kotlin.jvm' version kotlin_version apply false
        id 'org.jetbrains.kotlin.plugin.serialization' version kotlin_version apply false
        id 'com.r3.conclave.enclave' version conclaveVersion apply false
    }
}

include 'client'
include 'common'
include 'enclave'
include 'host'

