plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.4'
    id "io.spring.dependency-management" version "1.1.0"
}

// Override the default (mock) with -PenclaveMode=
def mode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "mock"

dependencies {
    runtimeOnly project(path: ":enclave", configuration: mode)
    // Use the host web server for receiving and sending mail to the clients. This means the client needs to use the
    // conclave-web-client library.
    runtimeOnly "com.r3.conclave:conclave-web-host:$conclaveVersion"

    // Change this to your preferred logging framework
    runtimeOnly 'ch.qos.logback:logback-classic'
}

springBoot {
    // Use the provided main method for the web host.
    mainClass.set("com.r3.conclave.host.web.EnclaveWebHost")
}

bootJar {
    // Include the enclave mode in the filename for clarity.
    archiveClassifier.set(mode)
}
